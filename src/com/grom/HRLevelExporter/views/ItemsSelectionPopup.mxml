<?xml version="1.0"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         title="Select Class">

    <fx:Script><![CDATA[
        import mx.collections.ArrayList;
        import mx.controls.Alert;
        import mx.core.FlexGlobals;
        import mx.managers.PopUpManager;

        private var _onSelectCompleted:Function;

        private var _levelClassesList:ArrayList;

        [Bindable]
        private var _filteredList:ArrayList;

        [Bindable]
        private var _allowMultiSelection:Boolean;

        public static function show(levelClasses:ArrayList, multiSelection:Boolean, selectionCompleted:Function):void
        {
            var win:ItemsSelectionPopup = new ItemsSelectionPopup();
            win._onSelectCompleted = selectionCompleted;
            win._levelClassesList = levelClasses;
            win._allowMultiSelection = multiSelection;
            win.updateFilter();
            PopUpManager.addPopUp(win, DisplayObject(FlexGlobals.topLevelApplication), true);
            PopUpManager.centerPopUp(win);
        }

        private function close():void
        {
            PopUpManager.removePopUp(this);
        }

        private function onOk():void
        {
            if (!classesList.selectedItems.length)
            {
                Alert.show("Select level class!");
                return;
            }

            var selectedClasses:Vector.<String> = new <String>[];
            for each (var levelClass:String in classesList.selectedItems)
            {
                selectedClasses.push(levelClass);
            }

            _onSelectCompleted(selectedClasses);
            close();
        }

        private function updateFilter(text:String = null):void
        {
            if (!text)
            {
                _filteredList = _levelClassesList;
            }
            else
            {
                var list:Array = [];

                for each (var name:String in _levelClassesList.toArray())
                {
                    if (name.toLowerCase().indexOf(text) >= 0)
                    {
                        list.push(name);
                    }
                }

                _filteredList = new ArrayList(list);
            }
        }
        ]]></fx:Script>

    <s:VGroup width="600" height="600" paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10">
        <s:List id="classesList" width="100%" height="100%" dataProvider="{_filteredList}"
                allowMultipleSelection="{_allowMultiSelection}"/>

        <s:HGroup width="100%">

            <s:HGroup verticalAlign="middle" width="100%">
                <s:Label text="Find:"/>
                <s:TextInput id="inputFind" width="100%" change="updateFilter(inputFind.text.toLowerCase())"/>
            </s:HGroup>

            <s:HGroup horizontalAlign="right">
                <s:Button label="OK" click="onOk()"/>
                <s:Button label="Close" click="close()"/>
            </s:HGroup>
        </s:HGroup>

    </s:VGroup>

</s:Panel>

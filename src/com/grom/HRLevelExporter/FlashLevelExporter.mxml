<?xml version="1.0"?>
<s:WindowedApplication
        xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:ms="http://ns.adobe.com/mxml/2009" xmlns:views="com.grom.HRLevelExporter.views.*"
        xmlns:levels="com.grom.HRLevelExporter.views.levels.*"
        applicationComplete="onApplicationCreated()">

    <ms:Script><![CDATA[
        import com.grom.HRLevelExporter.model.LevelModel;
        import com.grom.HRLevelExporter.project.LevelProject;
        import com.grom.HRLevelExporter.views.SelectLevelPopup;

        import mx.collections.ArrayList;
        import mx.controls.Alert;

        import com.grom.lib.debug.Log;
        import com.grom.lib.utils.UString;

        import robotlegs.bender.bundles.mvcs.MVCSBundle;
        import robotlegs.bender.extensions.contextView.ContextView;
        import robotlegs.bender.framework.api.IContext;

        import robotlegs.bender.framework.impl.Context;

        [Bindable] 
        public var project:LevelProject;

        private var _context:IContext;

        private function onApplicationCreated():void
        {
            _context = new Context()
                    .install(MVCSBundle)
                    .configure(LevelsAppConfig)
                    .configure(new ContextView(stage));
        }

        private function onClickSave():void
        {
            if (!project.fileName)
            {
                var file:File = new File();
                file.addEventListener(Event.SELECT, function ():void
                {
                    project.fileName = file.nativePath;
                    project.save();
                });
                file.browseForSave("Save Project");
            }
            else
            {
                project.save();
            }
        }

        private function onClickLoadSWF():void
        {
            var file:File = new File();
            file.addEventListener(Event.SELECT, function ():void
            {
                project.swfFileName.value = file.nativePath;
            });
            file.browseForOpen("Load levels SWF", [new FileFilter("SWF", "*.swf")]);
        }

        private function onClickAddLevel():void
        {
            SelectLevelPopup.show(project.classesList, function (modelsList:Array):void
            {
                for each (var model:LevelModel in modelsList)
                {
                    project.levelsList.addItem(model);
                }
            });
        }

        private function onClickRemoveLevel():void
        {
            if (listSelectedLevels.selectedItem)
            {
                project.levelsList.removeItem(listSelectedLevels.selectedItem);
            }
        }

        private function onClickExport():void
        {
            if (!project.exportPath.value)
            {
                var file:File = new File();
                file.addEventListener(Event.SELECT, function ():void
                {
                    project.exportPath.value = file.nativePath;
                    exportLevels();
                });
                file.browseForDirectory("Export To...");
            }
            else
            {
                exportLevels();
            }
        }

        private function exportLevels():void
        {
            Log.info("exporting levels to: " + project.exportPath.value);

            var list:ArrayList = project.levelsList.value;
            var selectedFiles:Array = list.toArray().sort(function (i1:LevelModel, i2:LevelModel):int
            {
                var diff:int = i1._levelDifficult - i2._levelDifficult;
                if (diff != 0)
                {
                    return diff;
                }

                var c1:String = i1._levelClass;
                var c2:String = i2._levelClass;

                if (c1 > c2)
                {
                    return 1;
                }
                else if (c1 < c2)
                {
                    return -1;
                }

                return 0;
            });
            var path:File = new File(project.exportPath.value);

            var levelNum:int = project.levelStartNum.value;
            for each (var lm:LevelModel in selectedFiles)
            {
                var levelName:String = "level" + UString.prefixZero(levelNum, 3) + ".xml";
                LevelExport.saveLevel(levelName, path, project.getLevelMovie(lm._levelClass));
                levelNum++;
            }

            Alert.show("All levels saved!");
        }

        private function onClickLoad():void
        {
            var file:File = new File();
            file.addEventListener(Event.SELECT, function ():void
            {
                project.fileName = file.nativePath;
                project.tryLoad();
            });
            file.browseForOpen("Open levels project", [new FileFilter("Project file", "*.xml")]);
        }

        private function onClickNew():void
        {
            project.fileName = null;
            project.newProject();
        }
        ]]></ms:Script>


    <s:VGroup width="100%" height="100%"
              paddingLeft="5"
              paddingRight="5"
              paddingTop="5"
              paddingBottom="5">
        <s:Label id="projectLabel" text="{project.fileName}"/>
        <s:HGroup width="100%" verticalAlign="middle">
            <s:Button label="New Project" click="onClickNew()"/>
            <s:Button label="Load..." click="onClickLoad()"/>
            <s:Button label="Save..." click="onClickSave()"/>
        </s:HGroup>
        <s:HGroup width="100%" verticalAlign="middle">
            <s:Button label="Levels SWF..." click="onClickLoadSWF()"/>
            <s:Label text="{project.swfFileName.value}"/>
        </s:HGroup>
        <s:HGroup width="100%" verticalAlign="middle">
            <s:Button label="Export..." click="onClickExport()"/>
            <s:Label id="exportPath" text="{project.exportPath.value}"/>
        </s:HGroup>
        <s:HGroup width="100%" height="100%">
            <s:VGroup height="100%">
                <s:List width="200" height="100%" dataProvider="{project.classesList}"/>
            </s:VGroup>

            <s:VGroup height="100%">
                <s:HGroup verticalAlign="middle">
                    <s:Button label="Add level" click="onClickAddLevel()"/>
                    <s:Button label="Remove level" click="onClickRemoveLevel()"/>
                    <s:Label text="Level start number: "/>
                    <s:NumericStepper id="levelStartNum"
                                      value="{project.levelStartNum.value}"
                                      minimum="0" maximum="1000"
                                      change="project.levelStartNum.value = levelStartNum.value"/>
                </s:HGroup>
                <levels:LevelsList id="listSelectedLevels" width="400" height="100%"
                                   itemRenderer="com.grom.HRLevelExporter.views.levels.LevelItemRenderer"
                                   dragEnabled="true" dropEnabled="true" dragMoveEnabled="true"
                                   dataProvider="{project.levelsList.value}"/>
            </s:VGroup>

            <views:LevelPreview id="levelPreview" width="100%" height="100%"/>

        </s:HGroup>

        <s:HGroup verticalAlign="middle">
            <s:Button id="buttonGameExec" label="Game Executable..."/>
            <s:Label text="{project.gamePath.value}"/>
        </s:HGroup>

        <s:HGroup verticalAlign="middle">
            <s:Button id="buttonWorkingFolder" label="Working Folder..."/>
            <s:Label text="{project.workFolder.value}"/>
        </s:HGroup>

    </s:VGroup>
</s:WindowedApplication>
